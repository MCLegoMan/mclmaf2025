plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id "com.github.breadmoirai.github-release" version "2.4.1"
	id "com.modrinth.minotaur" version "2.+"
	id 'maven-publish'
}
group = project.maven_group
version = project.mod_version + project.mc_version

base {
	archivesName = project.archives_base_name
}
runClient.doFirst {
	args += ['--username', "mclmaf2025${new Random().nextInt(1000)}"]
}
loom {
	mods {
		"mclmaf2025" {
			sourceSet("main")
		}
	}
}
repositories {
	maven { url 'https://jitpack.io' }
	maven { url 'https://maven.terraformersmc.com/releases/' }
	mavenCentral()
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}
dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft}"
	mappings "net.fabricmc:yarn:${project.mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api}"
	include(modImplementation "maven.modrinth:luminance:${project.luminance}")

	// Development Environment Only
	modImplementation "com.ptsmods:devlogin:3.4.1"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu}"
}
processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}
tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}
java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}
jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
	}
}
tasks.register("quickPlaySingleplayer") {
	group = 'mclmaf2025'
	description = 'Uses Quick Play to join singleplayer world `New World` on launch.'
	dependsOn(tasks.runClient.args(['--quickPlaySingleplayer', 'New World']))
}
tasks.register("quickPlayMultiplayer") {
	group = 'mclmaf2025'
	description = 'Uses Quick Play to join multiplayer server `localhost:25565` on launch.'
	dependsOn(tasks.runClient.args(['--quickPlayMultiplayer', 'localhost:25565']))
}
tasks.register("runClientLoggedIn") {
	group = 'mclmaf2025'
	description = 'Uses DevLogin to login to your Minecraft Account'
	dependsOn(tasks.runClient.args(['--msa']))
}
tasks.register("quickPlaySingleplayerLoggedIn") {
	group = 'mclmaf2025'
	description = 'Uses Quick Play to join singleplayer world `New World` on launch and uses DevLogin to login to your Minecraft Account.'
	dependsOn(tasks.runClient.args(['--quickPlaySingleplayer', 'New World', '--msa']))
}
tasks.register("quickPlayMultiplayerLoggedIn") {
	group = 'mclmaf2025'
	description = 'Uses Quick Play to join multiplayer server `localhost:25565` on launch and uses DevLogin to login to your Minecraft Account.'
	dependsOn(tasks.runClient.args(['--quickPlayMultiplayer', 'localhost:25565', '--msa']))
}